1  'CONTROLE DE ESTOQUE - 1990 v1.1
2 'LONGO SANTANA
3 '
4 DATACD,A8,00,32,77,F3,C9
5 CLEAR16000,&HF36F:ONERRORGOTO146:ONSTOPGOSUB147:STOPON:ONKEYGOSUB148,149,150,151:KEY(1)ON:KEY(2)ON:KEY(3)OFF:KEY(4)OFF:FORF=0TO6:READA$:POKE(&HF370+F),VAL("&H"+A$):NEXTF:DEFUSR=&HF370
6 SC=0:SCREEN0,,SC:WIDTH40:KEYOFF:COLOR7,1,1:CLS
7 DIMNM$(200),CD$(200),ES!(200),EM!(200),PU!(200):N!=0!
8 'MENU
9 CLS:GOSUB15:LOCATE11,5:PRINT"1> INCLUSAO":LOCATE11,6:PRINT"2> EXCLUSAO":LOCATE11,7:PRINT"3> MOVIMENTACAO":LOCATE11,8:PRINT"4> ALTERACAO":LOCATE11,9:PRINT"5> PESQUISA":LOCATE11,10:PRINT"6> LISTA/NOMES":LOCATE11,11:PRINT"7> TOTAL ESTOQUE/Cz$"
10 LOCATE11,12:PRINT"8> LISTA DE BAIXOS":LOCATE11,13:PRINT"9> LISTAGENS":LOCATE11,14:PRINT"0> GRAVACAO/LEITURA"
11 LOCATE6,18:PRINT"TECLE A OPCAO CORRESPONDENTE:"
12 STOP ON:Q$=INKEY$:IFQ$<>CHR$(27)AND(Q$<"0"ORQ$>"9")THEN12
13 IFQ$="0"THEN120ELSEIFQ$=CHR$(27)THEN140
14 ONVAL(Q$)GOTO21,35,49,59,68,81,91,97,106
15 LOCATE0,0:PRINT"XWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWYVMENU";TAB(26);"ITENS:";N!;TAB(39);"VZWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW["
16 LOCATE0,21:PRINT"W SOFT ESTOQUE WWWWWWWWWWLONGO SANTANA W":RETURN
17 FORI!=0!TO(N!-1!)
18 TM!=LEN(A1$):IFNM$(I!)=A1$THENRETURN
19 NEXTI!:LOCATE2,6:PRINT"ESTE PRODUTO NAO CONSTA DA LISTA!"
20 Q$=INKEY$:IFQ$<>CHR$(13)THEN20ELSEI!=N!:RETURN
21 'INCLUSAO
22 CLS:GOSUB15:LOCATE1,1:PRINT"INCLUSAO":A1$="":A2$="s/codigo":A3=0:A4=0:A5=0
23 IF N!<200!THEN25ELSELOCATE5,10:PRINT"NAO HA MAIS ESPACO NA MEMORIA!            SALVE OS DADOS E REINICIE!"
24 Q$=INKEY$:IFQ$<>CHR$(13)THEN24ELSE8
25 LOCATE2,4:PRINT"Nome? (max. 16 caracteres)":LOCATE2,5:LINEINPUTA1$:IFLEN(A1$)=0THEN8ELSEIFLEN(A1$)>16THENLOCATE0,5:PRINTSPACE$(2+LEN(A1$)):GOTO25
26 LOCATE0,5:PRINTSTRING$(40,32):LOCATE2,5:PRINTA1$:LOCATE2,7:PRINT"Codigo? (max 10 caracteres)":LOCATE2,8:LINEINPUTA2$:IFLEN(A2$)>10THENLOCATE0,8:PRINTSPACE$(2+LEN(A2$)):GOTO26
27 LOCATE0,8:PRINTSTRING$(40,32):LOCATE2,8:PRINTA2$:LOCATE2,10:PRINT"Estoque?":LOCATE0,11:INPUTA3!:LOCATE0,11:PRINTSTRING$(40,32):LOCATE1,11:PRINTA3!
28 LOCATE2,13:PRINT"Estoque minimo?":LOCATE0,14:INPUTA4!:LOCATE0,14:PRINTSTRING$(40,32):LOCATE1,14:PRINTA4!
29 LOCATE2,16:PRINT"Preco unitario?":LOCATE0,17:INPUTA5!:LOCATE0,17:PRINTSTRING$(40,32):LOCATE2,17:PRINT"Cr$ ";USING"##,###.##";A5!
30 LOCATE12,22:PRINT"CONFIRMA? (S/N)"
31 Q$=INKEY$:IFQ$<>"S"ANDQ$<>"N"ANDQ$<>"s"ANDQ$<>"n"THENLOCATE12,22:PRINT"               ":GOTO30
32 IFQ$="N"ORQ$="n"THEN21
33 NM$(N!)=A1$:CD$(N!)=A2$:ES!(N!)=A3!:EM!(N!)=A4!:PU!(N!)=A5!:N!=N!+1!
34 GOTO21
35 'EXCLUSAO
36 CLS:GOSUB15:LOCATE1,1:PRINT"EXCLUSAO"
37 LOCATE2,6: INPUT"Qual Produto";A1$:IFLEN(A1$)>16THEN35
38 GOSUB17:IFI!=N!THEN8ELSECLS:GOSUB15:LOCATE1,1:PRINT"EXCLUSAO":GOSUB117
39 LOCATE12,22:PRINT"CONFIRMA? (S/N)"
40 Q$=INKEY$:IFES!(I!)<EM!(I!)THENLOCATE25,10:PRINT"® BAIXO! ¯":LOCATE25,10:PRINT"          "
41 IF Q$="N"ORQ$="n"THEN8
42 IFQ$<>"s"ANDQ$<>"S"THEN40
43 FORF!=I!TON!-1!
44 NM$(F!)=NM$(F!+1!):CD$(F!)=CD$(F!+1!):ES!(F!)=ES!(F!+1!):EM!(F!)=EM!(F!+1!):PU!(F!)=PU!(F!+1!)
45 NEXTF!
46 N!=N!-1!
47 NM$(N!)=NM$(N!+1!):CD$(N!)=CD$(N!+1!):ES!(N!)=ES!(N!+1!):EM!(N!)=EM!(N!+1!):PU!(N!)=PU!(N!+1!)
48 GOTO8
49 'MOVIMENTACAO
50 CLS:GOSUB15:LOCATE1,1:PRINT"MOVIMENTACAO"
51 LOCATE2,6: INPUT"Qual Produto";A1$:IFLEN(A1$)>16THEN49
52 GOSUB 17:IFI!=N!THEN8ELSECLS:GOSUB15:LOCATE1,1:PRINT"MOVIMENTACAO"
53 GOSUB117
54 LOCATE 23,10:INPUTVA!:ES!(I!)=ES!(I!)+VA!
55 CLS:GOSUB15:LOCATE1,1:PRINT"MOVIMENTACAO":LOCATE14,3:PRINT"ESTOQUE ATUAL"
56 GOSUB117
57 LOCATE13,22:PRINT"TECLE <ENTER>"
58 Q$=INKEY$:IFQ$<>CHR$(13)THEN58ELSE8
59 'ALTERACAO
60 CLS:GOSUB15:LOCATE1,1:PRINT"ALTERACAO"
61 LOCATE2,6: INPUT"Qual Produto";A1$:IFLEN(A1$)>16THEN59
62 GOSUB 17:IFI!=N!THEN8ELSECLS:GOSUB15:LOCATE1,1:PRINT"ALTERACAO"
63 GOSUB117
64 LOCATE26,8:A$=CD$(I!):INPUTA$:CD$(I!)=A$:IFLEN(A$)<=10THENLOCATE2,8:PRINT"Codigo:    ";CD$(I!);SPACE$(25-LEN(CD$(I!)))ELSE64
65 LOCATE24,12:AL!=EM!(I!):INPUTAL!:EM!(I!)=AL!:LOCATE2,12:PRINT"Est. Min.:";EM!(I!);SPACE$(25-LEN(STR$(EM!(I!))))
66 LOCATE24,14:AL!=PU!(I!):INPUTAL!:PU!(I!)=AL!:LOCATE2,14:PRINT"Preco Unit.:Cr$ ";USING"###,###.##";PU!(I!):LOCATE24,14:PRINT"        ":LOCATE2,16:PRINT"TOTAL:Cr$  ";USING"###,###.##";PU!(I!)*ES!(I!)
67 GOTO57
68 'PESQUISA
69 CLS:GOSUB15:LOCATE1,1:PRINT"PESQUISA"
70 LOCATE2,6: INPUT"Qual Produto";A1$:IFLEN(A1$)>16THEN68
71 GOSUB 17:IFI!=N!THEN8
72 CLS:GOSUB15:LOCATE1,1:PRINT"PESQUISA"
73 GOSUB117
74 LOCATE10,19:PRINT"Setas: ver produtos"
75 IFES!(I!)<EM!(I!)THENLOCATE25,10:PRINT"          "
76 IFSTICK(0)=1ANDI!>0!THENI!=I!-1!:GOTO72
77 IFSTICK(0)=5ANDI!<250!AND(I!+1)<N!THENI!=I!+1!:GOTO72
78 IFINKEY$=CHR$(13)THEN8
79 IFES!(I!)<EM!(I!)THEN LOCATE25,10:PRINT"® BAIXO! ¯"
80 GOTO75
81 'LISTA/NOMES
82 CLS:GOSUB15:LOCATE1,1:PRINT"LISTA/NOMES":LOCATE2,5:IFN!=0!THENLOCATE11,10:PRINT"NAO HA REGISTROS!":GOTO57
83 N%=N!-1!:DIMA%(N%):FORG%=0TON%:A%(G%)=G%:NEXTG%:INPUT"EM ORDEM ALFABETICA? (S/N)";Q$:IFQ$="S"ORQ$="s"THENLOCATE8,8:PRINT"ORDENANDO...":GOSUB142
84 CLS:GOSUB15:LOCATE1,1:PRINT"LISTA/NOMES"
85 LOCATE2,5:T=2:FORI=0TON%STEP20:FORF=0TO19:IFI+F>N%THENERASEA%:GOTO57
86 PRINTTAB(T);NM$(A%(I+F));:IFT=22THENPRINT
87 IFT=2THENT=22ELSET=2
88 NEXTF
89 IFINKEY$<>CHR$(13)THEN89ELSECLS:GOSUB15:LOCATE1,1:PRINT"LISTA/NOMES":LOCATE2,5
90 NEXTI:ERASEA%:GOTO57
91 'TOTAL/ESTOQUE
92 CLS:GOSUB15:LOCATE1,1:PRINT"TOTAL/ESTOQUE"
93 LOCATE15,10:PRINT"AGUARDE":SM!=0
94 FORI!=0!TO(N!-1!):SM!=SM!+(PU!(I!)*ES!(I!)):NEXTI!
95 CLS:GOSUB15:LOCATE3,9:PRINT"O TOTAL DOS PRODUTOS EM ESTOQUE E:":LOCATE11,11:PRINT"Cr$ ";USING"###,###,###.##";SM!
96 GOTO57
97 'BAIXOS
98 CLS:GOSUB15:LOCATE1,1:PRINT"BAIXOS":LOCATE3,5
99 K$="":FORH%=0TO(N!-1):IF(ES!(H%)<EM!(H%))THENK$=K$+CHR$(H%)
100 NEXTH%:IFLEN(K$)=0THENLOCATE0,7:PRINT"NAO HA PRODUTOS ABAIXO DO ESTOQUE MINIMO":GOTO57
101 N%=LEN(K$)-1:DIMA%(N%):FORH%=0TO(LEN(K$)-1):A%(H%)=ASC(MID$(K$,(H%+1),1)):NEXTH%:LOCATE2,5:INPUT"EM ORDEM ALFABETICA? (S/N)";Q$:IFQ$="S"ORQ$="s"THEN:GOSUB142
102 CLS:GOSUB15:LOCATE1,1:PRINT"BAIXOS":LOCATE2,5:INPUT"VIDEO OU IMPRESSORA? (V/I)";Q$:IFQ$="I"ORQ$="i"THENG$="LISTA DE PRODUTOS EM BAIXA":GOTO107ELSECLS:GOSUB15:LOCATE1,1:PRINT"BAIXOS":FORJ!=0TOLEN(K$)-1:I!=A%(J!):GOSUB117:LOCATE12,20:PRINT"
103 LOCATE12,20:PRINT"<ENTER> continua"
104 IFINKEY$<>CHR$(13)THEN104
105 NEXTJ!:ERASEA%:LOCATE12,20:PRINT"                 ":GOTO57
106 CLS:GOSUB15:LOCATE1,1:PRINT"LISTAGEM":LOCATE2,5:PRINT"CONFIRMA LISTAGEM NA IMPRESSORA? (S/N)":LOCATE10,6:INPUTH$:IFH$="S"ORH$="s"THENN%=N!:G$="LISTA GERAL":DIMA%(N%):FORI=0TON%:A%(I)=I:NEXTI:ELSEIFH$<>"N"AND H$<>"n"THEN106ELSE8
107 REM
108 T!=0!:H%=INT(N%/52):FORI=0TOH%:LPRINTSTRING$(80,"#"):LPRINT"#   C O N T R O L E   D E   E S T O Q U E          L O N G O   S A N T A N A    #":LPRINTSTRING$(80,"#"):LPRINTG$;TAB(70);"PAGINA";I+1:LPRINT:LPRINT
109 LPRINT"                                         ESTOQUE ESTOQUE   PRECO        VALOR":LPRINT"NOME DO PRODUTO           CODIGO         ATUAL   MINIMO   UNITARIO      TOTAL"
110 LPRINTSTRING$(80,".")
111 FORG=0TO51:IF(I*52+G)=(N%+1)THEN116
112 J=(I*52+G):K=A%(J):IFNM$(K)<>""THENLPRINTNM$(K);TAB(24);CD$(K);TAB(40);ES!(K);TAB(48);EM!(K);TAB(56);USING"#,###.##";PU!(K);
113 IFNM$(K)<>""THENST!=PU!(K)*ES!(K):LPRINTTAB(68);USING"###,###.##";ST!:T!=T!+ST!
114 NEXTG
115 LPRINTSTRING$(80,"-"):LPRINTTAB(50);"A TRANSPORTAR:";USING"###,###,###.##";T!:LPRINT:LPRINT:LPRINT:NEXTI
116 LPRINTSTRING$(80,"-"):LPRINTTAB(58);"TOTAL:";USING"###,###,###.##";T!:LPRINTCHR$(12):ERASEA%:GOTO8
117 LOCATE2,6:PRINTSPACE$(36):LOCATE2,6:PRINT"Produto:   ";NM$(I!):LOCATE2,8:PRINTSPACE$(36):LOCATE2,8:PRINT"Codigo:    ";CD$(I!):LOCATE2,10:PRINTSPACE$(36):LOCATE2,10:PRINT"Estoque:  ";ES!(I!)
118 LOCATE2,12:PRINTSPACE$(36):LOCATE2,12:PRINT"Est. Min.:";EM!(I!):LOCATE2,14:PRINTSPACE$(36):LOCATE2,14:PRINT"Preco Unit.: Cr$";USING"###,###.##";PU!(I!):LOCATE2,16:PRINTSPACE$(36):LOCATE2,16:PRINT"TOTAL: Cr$ ";USING"###,###.##";PU!(I!)*ES!(I!)
119 RETURN
120 'SAVE/LOAD
121 KEY(3)ON:KEY(4)ON:CLS:GOSUB15:LOCATE1,1:PRINT"SAVE/LOAD":LOCATE13,6:PRINT"ESCOLHA :":LOCATE10,10:PRINT"1> CARREGAR DADOS":LOCATE10,11:PRINT"2> GRAVAR DADOS":LOCATE10,12:PRINT"3> VOLTAR AO MENU"
122 IFSC=2THENLOCATE6,15:PRINT"OPERANDO EM ALTA VELOCIDADE"
123 Q$=INKEY$:IFQ$="1"THEN124ELSEIFQ$="2" AND N!>0 THEN132ELSE:IFQ$="3"THENKEY(3)OFF:KEY(4)OFF:GOTO8ELSE123
124 'LOAD
125 KEY(3)OFF:KEY(4)OFF:GOSUB149:CLEAR16000:DIMNM$(250),CD$(250),ES!(250),EM!(250),PU!(250):CLS:GOSUB15:LOCATE1,1:PRINT"LOAD":N!=-1!:MOTORON:OPEN"CAS:LONG"FORINPUTAS#1
126 LOCATE2,8:PRINT"Numero de arquivos carregados: "
127 N!=N!+1!:LOCATE33,8:PRINTN!
128 IFEOF(1)THENMOTOROFF:CLOSE:GOTO130ELSEINPUT#1,NM$(N!),CD$(N!),ES!(N!),EM!(N!),PU!(N!)
129 GOTO127
130 LOCATE0,8:PRINTSTRING$(40,32):LOCATE9,8:PRINT"DADOS CARREGADOS:";N!
131 GOTO57
132 'SAVE
133 IF N!<0 THEN 123:KEY(3)OFF:KEY(4)OFF:GOSUB149:CLS:GOSUB15:LOCATE1,1:PRINT"SAVE":LOCATE4,6
134 PRINT"Gravando dados do arquivo n§":OPEN"CAS:LONG"FOROUTPUTAS#1
135 FORI=0TO(N!-1!):LOCATE29,6:PRINTI
136 PRINT#1,NM$(I);",";CD$(I);",";ES!(I);",";EM!(I);",";PU!(I)
137 NEXTI
138 CLOSE:LOCATE0,6:PRINTSTRING$(40,32):LOCATE9,8:PRINT"DADOS SALVOS:";I
139 GOTO57
140 'INICIALIZACAO
141 CLS:GOSUB15:LOCATE1,1:PRINT"REINICIALIZAR":LOCATE0,5:INPUT"Voce quer mesmo reinicializar? (S/N)";Q$:IFQ$="n"ORQ$="N"THEN8ELSECLEAR:DIMA%(250):RUN
142 G%=N%
143 IFG%=0THENBEEP:RETURNELSEG%=G%-1
144 FORH%=0TOG%:IFNM$(A%(H%))>NM$(A%(H%+1))THENQ%=A%(H%):A%(H%)=A%(H%+1):A%(H%+1)=Q%
145 NEXTH%:GOTO143
146 RESUME
147 RETURN
148 COLOR8,1,1:MOTORON:RETURN
149 COLOR7,1,1:MOTOROFF:RETURN
150 SC=2:SCREEN,,,SC:LOCATE6,15:PRINT"OPERACAO EM ALTA VELOCIDADE":RETURN
151 SC=1:SCREEN,,,SC:LOCATE6,15:PRINT"                           ":RETURN
152 GOSUB 15
153 LOCATE 5,9:PRINT"NAO HA DADOS PARA SEREM GRAVADOS"
154 GOSUB 15
155 GOTO 120
