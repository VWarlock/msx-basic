5 WIDTH 40:KEY OFF
10 GOTO 10000
1000 SCREEN0:WIDTH39:COLOR15,1,1:KEYOFF:MAXFILES=2:OPEN"GRP:"FOROUTPUTAS#1
1010 ON ERROR GOTO 1660
1020 DEFINTA-Z
1030 '
1040 ' Iniciar variaveis
1050 '
1060 X=128:Y=80:DIM P$(10),C$(100):NL=1:EX=0:PROMPT$=">":BEG=0
1070 '
1080 ' Recebe comandos
1090 '
1100 PRINTPROMPT$;:LINEINPUT L$:Q$=L$
1110 IF L$="" THEN 1100
1120 GOSUB 1170
1130 PRINT "Ok":GOTO 1100
1140 '
1150 ' Testa comandos
1160 '
1170 GOSUB 1410:L$=P$(1):GOSUB 13000:P$(1)=L$
1180 IF P$(1)="END" THEN BEG=0:PROMPT$=">":NL=NL-1:RETURN
1190 IF BEG=1 THEN C$(NL)=Q$:NL=NL+1:GOTO 1100
1200 L$=P$(1):GOSUB 13000
1210 IF L$="BEGIN" THEN BEG=1:NL=1:PROMPT$="   ":RETURN 1100
1220 IF L$="APPEND" THEN BEG=1:NL=NL+1:PROMPT$="   ":RETURN 1100
1230 IF L$="CLEARSCR" THEN CLS:RETURN
1240 IF L$="MODE" THEN WIDTH VAL(P$(2)):RETURN
1250 IF L$="POS" THEN 2020
1260 IF L$="DISPLAY" THEN 1970
1270 IF L$="VDPUT" THEN VPOKE VAL(P$(2)),VAL(P$(3)):RETURN
1280 IF L$="EXIT" THEN CLS:END
1290 IF L$="GRAPH" THEN 1740
1300 IF L$="TEXT" THEN 1800
1310 IF L$="SET" THEN 1540
1320 IF L$="WAIT" THEN A$=INPUT$(1):RETURN
1330 IF L$="EDIT" THEN PRINT"BEGIN":FOR Z=1 TO NL:PRINT "   ";C$(Z):NEXT:PRINT "END":RETURN
1340 IF L$="COLOR" THEN COLOR VAL(P$(2)),VAL(P$(3)),VAL(P$(4)):RETURN
1350 IF EX=1 THEN 1870
1360 ERROR 2
1370 RETURN
1380 '
1390 ' Separa parametros
1400 '
1410 NP=1:AS=0
1420 FOR Z=1 TO LEN(L$)
1430  A$=MID$(L$,Z,1)
1440  IF A$="'" AND AS=0 THEN AS=1:L$=MID$(L$,Z+1):GOTO 1420
1450  IF A$="'" AND AS=1 THEN AS=0:P$(NP)=MID$(L$,1,Z-1):NP=NP+1:L$=MID$(L$,Z+1):GOTO 1480
1460 IF AS=1 THEN 1480
1470  IF A$=" " THEN P$(NP)=LEFT$(L$,Z-1):L$=MID$(L$,Z+1):NP=NP+1:GOTO 1420
1480 NEXT
1490 P$(NP)=L$
1500 RETURN
1510 '
1520 ' Comando : SET
1530 '
1540 IF NL=0 THEN 1130
1550 EX=1
1560 FOR W=1 TO NL
1570  L$=C$(W)
1580  GOSUB 1170
1590 NEXT W
1600 EX=0
1610 IF PEEK(&HFCAF)=2 THEN SCREEN0
1620 RETURN1130
1630 '
1640 ' Trata erros
1650 '
1660 IF PEEK(&HFCAF)=2 THEN SCREEN0
1670 BEEP:EX=0
1680 IF ERR=2 THEN PRINT "Erro de sintaxe"
1690 IF ERR=5 THEN PRINT"Chamada ilegal de fun`„o"
1700 RESUME 1130
1710 '
1720 ' Comando : GRAPH
1730 '
1740 IF EX=0 THEN ERROR 5
1750 SCREEN2
1760 GOTO 1370
1770 '
1780 ' Comando : TEXT
1790 '
1800 V=VAL(P$(2))
1810 IF V<0 OR V>1 THEN ERROR 5
1820 SCREEN V
1830 GOTO 1370
1840 '
1850 ' Comandos que so operam com SET
1860 '
1870 IF L$="INITPOINT" THEN PRESET(VAL(P$(2)),VAL(P$(3))):RETURN
1880 IF L$="DEFXY" THEN LINE-(VAL(P$(2)),VAL(P$(3))):RETURN
1890 IF L$="FILL" THEN PAINT(VAL(P$(2)),VAL(P$(3))):RETURN
1900 IF L$="CIRCLE" THEN CIRCLE(VAL(P$(2)),VAL(P$(3))),VAL(P$(4)):RETURN
1910 IF L$="POINT" THEN PSET(VAL(P$(2)),VAL(P$(3))):RETURN
1920 IF L$="GO" THEN A=VAL(P$(2)):IF A>0 THEN W=A-1:RETURN1590 ELSE ERROR 5
1921 IF L$="PIXEL" THEN DRAW P$(2):RETURN
1930 ERROR 2
1940 '
1950 ' Comando : DISPLAY
1960 '
1970 IF PEEK(&HFCAF)=2 THEN PRINT#1,P$(2) ELSE PRINT P$(2)
1980 GOTO1370
1990 '
2000 ' Comando : POS
2010 '
2020 PRINT "Linha anterior ";C$(VAL(P$(2)))
2030 PRINT "Nova linha ";:LINEINPUT C$(VAL(P$(2)))
2040 RETURN
10000 '
10010 ' Abertura
10020 '
10025 CLS
10030 LOCATE 0,0:PRINT"XWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWY "
10040 FOR Y=1 TO 20
10050 LOCATE 0,Y:PRINT"V                                    V "
10060 NEXT
10070 LOCATE 0,21:PRINT"ZWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWW[ "
10090 FORY=19TO1STEP-1
10100 LOCATE 6,Y:PRINT"Graphic Language Programable"
10110 FORT=0TO10:NEXTT
10120 LOCATE 6,Y:PRINT"                            "
10130 FOR T=0 TO 10:NEXT T
10140 NEXT Y
10145 LOCATE 6,1:PRINT"Graphic Language Programable"
10150 LOCATE 1,19:PRINT"(c)Copyright 1992,by Bit & Byte Inc."
10160 C=8:L=9:A$="by Bit & Byte Software":GOSUB 11000:GOSUB 12000
10170 GOTO 1000
11000 FOR T=1 TO LEN(A$)
11010 L$=MID$(A$,T,1)
11020 LOCATE C+T,L:PRINTL$
11030 FOR F=1 TO 20:NEXT F
11040 NEXT T
11050 RETURN
12000 LOCATE C+T,L:PRINT"À"
12010 FOR J=1 TO 90:NEXT J
12020 LOCATE C+T,L:PRINT" "
12030 FOR J=1 TO 90:NEXT J
12200 IF INKEY$="" THEN 12000
12210 RETURN
13000 '
13010 ' Upper
13020 '
13030 FOR T=1 TO LEN(L$)
13040 LE$=MID$(L$,T,1)
13050 LE=ASC(LE$)
13060 IF LE>96 AND LE<123 THEN LE=LE XOR 32:MID$(L$,T,1)=CHR$(LE)
13070 NEXT
13080 RETURN
